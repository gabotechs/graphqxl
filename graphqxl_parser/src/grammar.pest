spec = { SOI ~ def* ~ EOI }

def = _{ type_def | input_def | enum_def | interface_def | scalar_def | union_def | directive_def }
type_def = { description? ~ "type " ~ identifier ~ type_selection_set }
    type_selection_set = { "{" ~ field_with_args* ~ "}" }
input_def = { description? ~ "input " ~ identifier ~ input_selection_set }
    input_selection_set = { "{" ~ field_without_args* ~ "}" }
enum_def = { description? ~ "enum " ~ identifier ~ enum_selection_set }
    enum_selection_set = { "{" ~ field_without_args_without_value* ~ "}" }
interface_def = { description? ~ "interface " ~ identifier ~ interface_selection_set }
    interface_selection_set = { "{" ~ field_with_args ~ "}" }
scalar_def = { description? ~ "scalar " ~ identifier }
union_def = { description? ~ "union " ~ identifier ~ "=" ~ identifier ~ ("|" ~ identifier )* }
directive_def = { description? ~"directive" ~ "@" ~ identifier ~ arguments? ~ directive_repeatable? ~ "on" ~ directive_location ~ ("|" ~ directive_location)* }
    directive_repeatable = @{ "repeatable" }
    directive_location = @{
        "QUERY" |
        "MUTATION" |
        "SUBSCRIPTION" |
        "FIELD_DEFINITION" |
        "FIELD" |
        "FRAGMENT_DEFINITION" |
        "FRAGMENT_SPREAD" |
        "INLINE_FRAGMENT" |
        "SCHEMA" |
        "SCALAR" |
        "OBJECT" |
        "ARGUMENT_DEFINITION" |
        "INTERFACE" |
        "UNION" |
        "ENUM_VALUE" |
        "ENUM" |
        "INPUT_OBJECT" |
        "INPUT_FIELD_DEFINITION" |
        "VARIABLE_DEFINITION"
    }

field_with_args = { description? ~ identifier ~ arguments? ~ ":" ~ value_type }
field_without_args = { description? ~ identifier ~ ":" ~ value_type }
field_without_args_without_value = { description? ~ identifier }

arguments = { "(" ~ argument* ~ ")" }
    argument = { description? ~ identifier ~ ":" ~ value_type ~ ("=" ~ value_data)? }

function_call = { "(" ~ function_input+ ~ ")" }
    function_input = { identifier ~ ":" ~ value_data }

description = @{ multiline_description | one_line_description }
    multiline_description = @{ "\"\"\"" ~ char*  ~ "\"\"\"" }
    one_line_description = @{ "\"" ~ (!("\n" | "\r\n") ~ char)* ~ "\"" }

directive = { "@" ~ identifier ~ function_call? ~ ":" ~ value_data }

value_type = { value_array_non_nullable | value_array_nullable | value_non_nullable | value_nullable }

    value_array_non_nullable = { value_array_nullable ~ "!" }
    value_array_nullable = { "[" ~ (value_non_nullable | value_nullable) ~ "]" }
    value_non_nullable = { value_nullable ~ "!" }
    value_nullable = { value_basic_type }

        value_basic_type = { object | basic_type }
            int = @{ "Int" }
            float = @{ "Float" }
            string = @{ "String" }
            boolean = @{ "Boolean" }
            basic_type = _{ int | float | boolean | string }
            object = @{ &((basic_type ~ ASCII_ALPHANUMERIC | "_") | !basic_type) ~ identifier }

value_data = { object_data | list_data | basic_data }
    object_data = { "{" ~ object_entry* ~ "}" }
    object_entry = { identifier ~ ":" ~ (basic_data | list_data | object_data) }
    list_data = { "[" ~ (basic_data | list_data | object_data)* ~ "]" }
        basic_data = { float_data | int_data | string_data | boolean_data }
        int_data = @{ ASCII_DIGIT+ }
        float_data = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+}
        string_data = @{ "\"" ~ char* ~ "\"" }
        boolean_data = @{ "false" | "true" }

char = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
identifier = @{ ( ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" )* }
WHITESPACE = _{ " " | "," | "\n" | "\r" | "\t" }
