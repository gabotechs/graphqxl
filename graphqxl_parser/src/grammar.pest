spec = _{ SOI ~ def* ~ EOI }

def = { type_def | input_def }
type_def = { "type" ~ identifier ~ type_selection_set }
    type_selection_set = { "{" ~ field_with_args* ~ "}" }
input_def = { "input" ~ identifier ~ input_selection_set }
    input_selection_set = { "{" ~ field_without_args* ~ "}" }
enum_def = { "enum" ~ identifier ~ enum_selection_set }
    enum_selection_set = { "{" ~ field_without_args_without_value* ~ "}" }
interface_def = { "interface" ~ identifier ~ interface_selection_set }
    interface_selection_set = { "{" ~ field_with_args ~ "}" }
scalar_def = { "scalar" ~ identifier }
union_def = { "union" ~ identifier ~ "=" ~ identifier ~ ("|" ~ identifier )* }

field_with_args = { identifier ~ arguments? ~ ":" ~ value }
field_without_args = { identifier ~ ":" ~ value }
field_without_args_without_value = { identifier }

directive_def = { "directive" ~ "@" ~ identifier ~ arguments? ~ directive_repeatable? ~ "on" ~ directive_location ~ ("|" ~ directive_location)* }
    directive_repeatable = @{ "repeatable" }
    directive_location = @{
        "QUERY" |
        "MUTATION" |
        "SUBSCRIPTION" |
        "FIELD_DEFINITION" |
        "FIELD" |
        "FRAGMENT_DEFINITION" |
        "FRAGMENT_SPREAD" |
        "INLINE_FRAGMENT" |
        "SCHEMA" |
        "SCALAR" |
        "OBJECT" |
        "ARGUMENT_DEFINITION" |
        "INTERFACE" |
        "UNION" |
        "ENUM_VALUE" |
        "ENUM" |
        "INPUT_OBJECT" |
        "INPUT_FIELD_DEFINITION" |
        "VARIABLE_DEFINITION"
    }

arguments = { "(" ~ argument ~ ("," ~ argument)* ~ ")"}
    argument = { identifier ~ ":" ~ value }

value = { value_array_non_nullable | value_array_nullable | value_non_nullable | value_nullable }

    value_array_non_nullable = { value_array_nullable ~ "!" }
    value_array_nullable = { "[" ~ (value_non_nullable | value_nullable) ~ "]" }
    value_non_nullable = { value_nullable ~ "!" }
    value_nullable = { value_content }

        value_content = { object | basic_type }
            int = @{ "Int" }
            float = @{ "Float" }
            string = @{ "String" }
            boolean = @{ "Boolean" }
            basic_type = _{ int | float | boolean | string }
            object = @{ &((basic_type ~ ASCII_ALPHANUMERIC | "_") | !basic_type) ~ identifier }


identifier = @{ ( ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" )+ }
WHITESPACE = _{ " " | "\n" | "\r" | "\t" }
